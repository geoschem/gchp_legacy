function tpw (args)
                                                                                                                            
expid    = subwrd(args,1)
output   = subwrd(args,2)
debug    = subwrd(args,3)
                                                                                                                            

* Initialize
* ----------
'reinit'
'set display color white'
'set clab off'
'c'

* Get Environment Variables
* -------------------------
'run getenv "GEOSUTIL"'
         geosutil = result
                                                                                                                            

* Get Model Variables
* -------------------
'run getvar TQV AGCM'
        qname = subwrd(result,1)
        qfile = subwrd(result,2)
        scale = subwrd(result,3)
       expdsc = subwrd(result,4)
    if( qname = 'NULL' ) ; return ; endif

* Compute Time Series
* -------------------
'set lon 0 360'
'set lat -90 90'
'set z 1'

'setdates'
'run getenv "BEGDATE"'
             begdate  = result
'run getenv "ENDDATE"'
             enddate  = result
'sett'
'define dum  = 'qname
'define mod  = regrid2( dum,1,1,bs_p1,0,-90)'
'define modg = aave( mod,global )'


*****************************************************************
****  Loop over Possible Experiment Datasets for Comparison  ****
*****************************************************************

'!/bin/mv HISTORY.T HISTORY.Tmp'
'run getenv "CMPEXP"'
         cmpexp = result
            num = 1
            exp = subwrd( cmpexp,num )
while( exp != 'NULL' )
say ' '
say 'Comparing with: 'exp

'run setenv "LEVTYPE" 'ALEVS

'!chckfile 'exp'/.HOMDIR'
 'run getenv CHECKFILE'
         CHECKFILE  = result
     if( CHECKFILE != 'NULL' )
        '!/bin/cp `cat 'exp'/.HOMDIR`/HISTORY.rc .'
     else
        '!/bin/cp 'exp'/HISTORY.rc .'
     endif
'!remove CHECKFILE.txt'

'!cat HISTORY.rc | sed -e "s/,/ , /g" > HISTORY.T'

   'run getvar TQV AGCM 'exp
           oname = subwrd(result,1)
         obsfile = subwrd(result,2)
          oscale = subwrd(result,3)
          obsdsc = subwrd(result,4)
          obsnam = subwrd(result,5)

if( oname != 'NULL' )
'set dfile 'obsfile
'set z 1'
'set lon 0 360'
'set lat -90 90'
'getdates'

'define dum = 'oname
'define dum = dum*'oscale

'define    obs = regrid2( dum,1,1,bs_p1,0,-90)'
'define modobs = maskout( mod, abs(obs) )'

'set dfile 'qfile
'getdates'
'define mTPWg = aave( modobs,global )'

'set dfile 'obsfile
'getdates'
'define oTPWg = aave( obs,global )'

'set dfile 'qfile
'set x 1'
'set y 1'
'set z 1'

* Make Plot
* ---------
        flag = ""
while ( flag = "" )
       'run 'geosutil'/plots/tseries/tpw_1 'expid' 'expdsc' 'output' 'obsnam' 'obsfile
        if( debug = "debug" )
            say "Hit  ENTER  to repeat plot"
            say "Type 'next' for  next plot, 'done' for next field"
                 pull flag
        else
                 flag = "next"
        endif
        'c'
endwhile

endif

* Check next Comparison Experiment Dataset
* ----------------------------------------
  num = num + 1
  exp = subwrd( cmpexp,num )
endwhile
'!/bin/mv HISTORY.Tmp HISTORY.T'

*******************************************************************
****  Loop over Possible Verification Datasets for Comparison  ****
*******************************************************************

'getnumrc 'geosutil'/plots/tseries'
     rcinfo = result
     numrc  = subwrd( rcinfo,1 )

         k  = 1
while(   k <= numrc )
        loc = k + 1
     rcfile = subwrd( rcinfo,loc )
'run getobs TQV AGCM 'rcfile
          oname = subwrd(result,1)
        obsfile = subwrd(result,2)
         oscale = subwrd(result,3)
         obsdsc = subwrd(result,4)
         obsnam = subwrd(result,5)

if( oname != 'NULL' )
'set dfile 'obsfile
'set z 1'
'set lon 0 360'
'set lat -90 90'
'getdates'

'define dum = 'oname
'define dum = dum*'oscale

'define    obs = regrid2( dum,1,1,bs_p1,0,-90)'
'define modobs = maskout( mod, abs(obs) )'

'set dfile 'qfile
'getdates'
'define mTPWg = aave( modobs,global )'

'set dfile 'obsfile
'getdates'
'define oTPWg = aave( obs,global )'

'set dfile 'qfile
'set x 1'
'set y 1'
'set z 1'

* Make Plot
* ---------
        flag = ""
while ( flag = "" )
       'run 'geosutil'/plots/tseries/tpw_1 'expid' 'expdsc' 'output' 'obsnam' 'obsfile
        if( debug = "debug" )
            say "Hit  ENTER  to repeat plot"
            say "Type 'next' for  next plot, 'done' for next field"
                 pull flag
        else
                 flag = "next"
        endif
        'c'
endwhile

endif
k = k + 1
endwhile
