#!/bin/tcsh -f
alias MATH 'set \!:1 = `echo "\!:3-$" | bc -l`'

if ( ! $?GEOSUTIL ) then
      echo " "
      echo Environment variable GEOSUTIL must be defined before use!
      echo Set GEOSUTIL to the directory path of the GEOS_util module
      echo " "
      exit 1
endif

@ nmax = $#argv

# -------------------
if( $nmax == 0 ) then
Usage:
      echo " "
      echo "Usage:"
      echo "-----"
      echo "quickstat -rc       STATS_RC"
      echo "         [-exps     EXPS]"
      echo "         [-level    LEVEL]"
      echo "         [-fields   FIELDS]"
      echo "         [-plots    PLOTS]"
      echo "         [-output   OUTPUT]"
      echo "         [-gradsx   GRADSX]"
      echo "         [-batch          ]"
      echo "         [-partition PARTITION]"
      echo "         [-qos       QOS]"
      echo " "
      echo "where:    STATS_RC: is the full path (including filename) of the stats resource file to use         "
      echo "              EXPS: Experiment IDs to plot (e.g., 0 1 3 4 )  (Default: ALL experiments in STATS_RC) "
      echo "             LEVEL: Pressure Level to plot (e.g., 850 )      (Default: ALL Levels)                  "
      echo "            FIELDS: Fields to plot (e.g., p u v )            (Default: p u v t q h)                 "
      echo "             PLOTS: Plot Types (corcmp, sys, syscmp)         (Default: ALL)                         "
      echo "            OUTPUT: Output directory to contain plots        (Default: STATS_RC directory)          "
      echo "            GRADSX: GrADS executable to use for processing   (Default: grads)                       "
      echo " "
      echo "Note:    Plots will be located in experiment sub-directories under OUTPUT                           "
      echo "         Usage of  -batch option will send plots to BATCH                                           "
      echo " "
      exit 1
endif


# Set Variables
# -------------
set QUEUE     = partition
set PARTITION = compute
set batch     = NULL
set statsrc   = NULL
set output    = NULL
set gradsx    = grads
set exps      = ALL
set level     = ALL
set plots     = `echo corcmp pltsys syscmp`
set fields    = `echo p u v t q h`

set lcd = `pwd`

# Parse command line
# ------------------

@       n  = 1
while( $n <= $nmax )
       if( "$argv[$n]" == "-rc" ) then
                    @ n = $n + 1
             set statsrc  = $argv[$n]
       endif
       if( "$argv[$n]" =~ \-lev* ) then
                    @ n = $n + 1
             set  level = $argv[$n]
       endif
       if( "$argv[$n]" =~ \-b* ) then
             set batch  = 'BATCH'
       endif
       if( "$argv[$n]" == "-partition" ) then
                    @ n = $n + 1
             set PARTITION = $argv[$n]
       endif
       if( "$argv[$n]" == "-qos" ) then
                    @ n = $n + 1
             set PARTITION = $argv[$n]
             set QUEUE     = 'qos'
       endif
       if( "$argv[$n]" == "-gradsx"   ) then
                    @ n = $n + 1
             set gradsx = $argv[$n]
       endif
       if( "$argv[$n]" == "-output"   ) then
                    @ n = $n + 1
             set output = $argv[$n]
       endif
       if( "$argv[$n]" =~ \-exp*  ) then
                    @ n = $n + 1
                                      set exps = ""
                                      set next   = $argv[$n]
                                      set bit    = `echo $next | cut -c1-1`
                                      while( "$bit" != "-" )
                                         set exps = `echo $exps $next`
                                          @   n  = $n + 1
                                         if( $n <= $nmax ) then
                                             set next = $argv[$n]
                                             set bit  = `echo $next | cut -c1-1`
                                         else
                                             set bit  = "-"
                                         endif
                                      end
                                             @ n = $n - 1
       endif
       if( "$argv[$n]" =~ \-plot* ) then
                    @ n = $n + 1
                                      set plots = ""
                                      set   next  = $argv[$n]
                                      if( "$next" == "sys" ) set next = pltsys
                                      set bit    = `echo $next | cut -c1-1`
                                      while( "$bit" != "-" )
                                         set plots = `echo $plots $next`
                                          @   n  = $n + 1
                                         if( $n <= $nmax ) then
                                             set   next  = $argv[$n]
                                             if( "$next" == "sys" ) set next = pltsys
                                             set bit  = `echo $next | cut -c1-1`
                                         else
                                             set bit  = "-"
                                         endif
                                      end
                                             @ n = $n - 1
       endif
       if( "$argv[$n]" =~ \-field*  ) then
                    @ n = $n + 1
                                      set fields = ""
                                      set next   = $argv[$n]
                                      set bit    = `echo $next | cut -c1-1`
                                      while( "$bit" != "-" )
                                         set fields = `echo $fields $next`
                                          @   n  = $n + 1
                                         if( $n <= $nmax ) then
                                             set next = $argv[$n]
                                             set bit  = `echo $next | cut -c1-1`
                                         else
                                             set bit  = "-"
                                         endif
                                      end
                                             @ n = $n - 1
       endif
     @ n = $n + 1
end

if( $statsrc  == NULL | -d $statsrc ) then
     echo ' '
     echo '\!\! You must enter the full path (including filename) of the stats.rc to use \!\!'
     goto Usage
     exit
endif

set rcfile = `basename $statsrc`
setenv GRADSX $gradsx 


# Determine ROOT directory containing stats.rc
# --------------------------------------------
@ n = 1
set root = `echo $statsrc | cut -d / -f$n`
while( .$root == . )
@ n = $n + 1
set root = `echo $statsrc | cut -d / -f$n`
end

set ROOTDIR = '' 
while( $root != $rcfile )
set ROOTDIR = `echo ${ROOTDIR}/${root}`
@ n = $n + 1 
set root = `echo $statsrc | cut -d / -f$n`
end


# Set SOURCE directory containing Plots
# -------------------------------------
if( $output == NULL ) then
    setenv SOURCE $ROOTDIR
else
    if(! -e $output ) mkdir -p $output
    setenv SOURCE $output
    if(-e $output/$rcfile ) /bin/rm -f $output/$rcfile
    /bin/ln -sf $statsrc $output
endif



# Determine Experiments to plot
# -----------------------------
if( $exps[1] == ALL ) then
 set allexps = `grep "EXP.*:" $statsrc | grep -v \#`
 @   numexps = $#allexps / 2
 set    exps = ''
 @   n = 1
 while ($n <= $numexps )
   @ loc = $n * 2 - 1
   set len1 = `echo $allexps[$loc] | awk '{print length($0)}'`
   @   len2 = $len1 - 1
   set exp  = `echo $allexps[$loc] | cut -c4-$len2`
   set exps = `echo $exps $exp`
   @      n = $n + 1
 end
endif


# Configure QUICKSTAT to Computing Platform
# -----------------------------------------
cd $SOURCE
if( -e LOCKFILE ) /bin/echo "Waiting for LOCKFILE ... "
       lockfile LOCKFILE
$GEOSUTIL/plots/configure
source $SOURCE/.quickplotrc
     /bin/rm -f LOCKFILE

echo " "
echo "        ARCH = " `uname`
echo "        HOST = " `hostname`
echo "       GRADS = " $GRADS
echo "    GEOSUTIL = " $GEOSUTIL
echo "      OUTPUT = " $SOURCE
echo "    STATS_RC = " $statsrc
echo "        EXPS = " $exps
echo "       LEVEL = " $level
echo "      FIELDS = " $fields
echo "       PLOTS = " $plots
echo " "
 
if( $level == ALL ) set level = NULL

# Check for BATCH Job
# -------------------
if( $batch == 'BATCH' ) then

# Set Default GEOS Build Directories
# ----------------------------------
                set   command = `which $0`
set   root =   `echo $command | cut -d / -f1`
if(  $root == . ) set command = `echo $lcd`/quickstat
  set root =   `echo $command | cut -b1`
if( "$root" != "/" ) set command = `echo $lcd`/$command

@ n = 1
set root = `echo $command | cut -d / -f$n`
while( .$root == . )
@ n = $n + 1
set root = `echo $command | cut -d / -f$n`
end

set GEOSDEF = ''
while( $root != 'src' )
set GEOSDEF = `echo ${GEOSDEF}/${root}`
@ n = $n + 1
set root = `echo $command | cut -d / -f$n`
end
set  GEOSSRC = ${GEOSDEF}/src
set  GEOSBIN = ${GEOSDEF}/Linux/bin
set  GEOSAPP = ${GEOSDEF}/src/Applications/GEOSgcm_App


# Create QUICKSTAT commands
# -------------------------
set NPLOTS  = $#plots
set NEXPS   = $#exps
set NFIELDS = $#fields

echo "      NPLOTS = " $NPLOTS
echo "       NEXPS = " $NEXPS
echo "     NFIELDS = " $NFIELDS
echo " "

  @ SUBTASKS = 0
  @ n = 0
  foreach FIELD ($fields)
  @ n = $n + 1
    if( $n == 3 ) then
        @ SUBTASKS = $SUBTASKS + 1
        @ n = 0
    endif
  end
if( $n != 0 ) then
    @ SUBTASKS = $SUBTASKS + 1
endif

set quickstat_corcmp = ''
set quickstat_pltsys = ''
set quickstat_syscmp = ''

@ NTASKS = 0
foreach plot ($plots)
   if( $plot == 'corcmp' ) then
       set quickstat_corcmp = `echo "./quickstat -rc $statsrc -plots corcmp -fields $fields"`
     @ CORCMPID = $NTASKS + 1
  MATH NTASKS   = $NTASKS + 1
       echo CORCMP: NTASKS = $NTASKS
   endif
   if( $plot == 'pltsys' ) then
       set quickstat_pltsys = `echo "./quickstat -rc $statsrc -plots sys    "`
     @ PLTSYSID = $NTASKS + 1
  MATH NTASKS   = $NTASKS + ( $NEXPS * $SUBTASKS )
      echo PLTSYS: NTASKS = $NTASKS   \( SUBTASKS:$SUBTASKS x NEXPS: $NEXPS \)
   endif
   if( $plot == 'syscmp' ) then
       set quickstat_syscmp = `echo "./quickstat -rc $statsrc -plots syscmp "`
       set syscmpexps = ''
       foreach exp ($exps)
       if( $exp != 0 ) set syscmpexps = `echo $syscmpexps $exp`
       end
       set NSYSCMPEXPS = $#syscmpexps
     @ SYSCMPID = $NTASKS + 1
  MATH NTASKS   = $NTASKS + ( $NSYSCMPEXPS * $SUBTASKS )
       echo SYSCMP: NTASKS = $NTASKS   \( SUBTASKS:$SUBTASKS x NEXPS:$NSYSCMPEXPS \)
   endif
end
echo " "

/bin/rm -f $SOURCE/gcm_quickstat.tmpl
touch      $SOURCE/gcm_quickstat.tmpl
cat        $GEOSAPP/gcm_quickstat.j >> $SOURCE/gcm_quickstat.tmpl

if( $#quickstat_corcmp > 1 ) then
      @ IDX = $CORCMPID
      echo 'if( $ID == '$IDX' ) '$quickstat_corcmp                            >> $SOURCE/gcm_quickstat.tmpl
      echo " "                                                                >> $SOURCE/gcm_quickstat.tmpl
endif
if( $#quickstat_pltsys > 1 ) then
      @ IDX = $PLTSYSID
      foreach EXP  ($exps)
      @ TASK = 1
      while( $TASK <= $SUBTASKS )
MATH  BEGFIELD = 1 + ( $TASK - 1 ) * 3 
MATH  ENDFIELD = $BEGFIELD   + 2
 if( $ENDFIELD > $NFIELDS ) set ENDFIELD = $NFIELDS
      set FIELDLIST = `echo $fields[${BEGFIELD}-${ENDFIELD}]`
      echo 'if( $ID == '$IDX' ) '$quickstat_pltsys" -field $FIELDLIST -exp $EXP"  >> $SOURCE/gcm_quickstat.tmpl
      @ TASK = $TASK + 1
      @ IDX  = $IDX  + 1
      end
      end
      echo " "                                                                    >> $SOURCE/gcm_quickstat.tmpl
endif
if( $#quickstat_syscmp > 1 ) then
      @ IDX = $SYSCMPID
      foreach EXP  ($syscmpexps)
      @ TASK = 1
      while( $TASK <= $SUBTASKS )
MATH  BEGFIELD = 1 + ( $TASK - 1 ) * 3 
MATH  ENDFIELD = $BEGFIELD   + 2
 if( $ENDFIELD > $NFIELDS ) set ENDFIELD = $NFIELDS
      set FIELDLIST = `echo $fields[${BEGFIELD}-${ENDFIELD}]`
      echo 'if( $ID == '$IDX' ) '$quickstat_syscmp" -field $FIELDLIST -exp $EXP"  >> $SOURCE/gcm_quickstat.tmpl
      @ TASK = $TASK + 1
      @ IDX  = $IDX  + 1
      end
      end
      echo " "                                                                    >> $SOURCE/gcm_quickstat.tmpl
endif

/bin/rm -f $SOURCE/gcm_quickstat.j
/bin/rm -f $SOURCE/sedfile
touch      $SOURCE/sedfile
cat >      $SOURCE/sedfile << EOF
s?@GEOSBIN?$GEOSBIN?g
s?@GEOSSRC?$GEOSSRC?g
s?@NTASKS?$NTASKS?g
s?@PARTITION?$PARTITION?g
s?@QUEUE?$QUEUE?g
EOF
sed -f $SOURCE/sedfile $SOURCE/gcm_quickstat.tmpl > $SOURCE/gcm_quickstat.j

echo "Please Submit: $SOURCE/gcm_quickstat.j  for processing"
echo " "
exit
endif


# Run Grads-Based Quickstat Utility for Anomaly Correlation Comparison
# --------------------------------------------------------------------
if( "$plots" =~ *corcmp* ) then
     set       tmpdir = ""
     set       pltdir = $SOURCE/scratch_corcmp
     mkdir -p $pltdir
     cd       $pltdir ; set tmpdir = `echo $tmpdir $pltdir`
     $GRADS -l -b -c "run $GEOSUTIL/plots/fcst/pltcorcmp -source $SOURCE -rc $rcfile -fields $fields" &
     wait
    /bin/rm -r $tmpdir
endif


# Run Grads-Based Quickstat Utility for Systematic Error
# ------------------------------------------------------
if( "$plots" =~ *pltsys* ) then
     set tmpdir = ""
     foreach exp   ($exps)
     foreach field ($fields)
        set       pltdir = $SOURCE/scratch_pltsys/EXP${exp}_$field.`date +%Y%m%d_%H%M%S`
        mkdir -p $pltdir
        cd       $pltdir ; set tmpdir = `echo $tmpdir $pltdir`
        $GRADS -l -b -c "run pltsys.gs -exp $exp -field $field -rc $rcfile -level $level" &
        sleep 5
     end
     end
     wait
    /bin/rm -r $tmpdir
endif


# Run Grads-Based Quickstat Utility for Systematic Error Comparison
# -----------------------------------------------------------------
if( "$plots" =~ *syscmp* ) then
     set tmpdir = ""
     foreach exp   ($exps)
     if( $exp != 0 ) then
         foreach field ($fields)
            set       pltdir = $SOURCE/scratch_syscmp/EXP${exp}_$field.`date +%Y%m%d_%H%M%S`
            mkdir -p $pltdir
            cd       $pltdir ; set tmpdir = `echo $tmpdir $pltdir`
            $GRADS -l -b -c "run $GEOSUTIL/plots/fcst/setup_statd -exps 0 $exp -fields $field -rc $rcfile -levels $level" &
            sleep 5
         end
     endif
     end
     wait
    /bin/rm -r $tmpdir
endif


# Sleep for 1 minutes to ensure all plots are finished
# ----------------------------------------------------
sleep 1m

# Reset Environment
# -----------------
cd $SOURCE

exit
